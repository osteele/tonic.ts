#!/usr/bin/env coffee

fs = require('fs')
program = require('commander')

FretboardLogic = require('../index')
{Chords} = FretboardLogic.theory
Layout = FretboardLogic.utils.layout

{
  chord_book,
  chord_fingerings_page,
  intervals_book
} = require('../lib/books')

chord_shape_flipbook = require('../lib/chord-shape-animation')

program
  .version('0.0.1')

with_book_options = (options, fn) ->
  dir = './build/'
  try
    fs.mkdirSync(dir)
  catch err
    throw err unless err.code == 'EEXIST'
  Layout.directory dir
  fn()

program
  .command('chordbook')
  .description('Create a chord bible')
  .option('-a, --combined', 'Combine all fingerings onto each chord chart')
  .option("-e, --exec_mode <mode>", "Which exec mode to use")
  .option('--pages <count>', 'Limit to this many pages', parseInt)
  .action (options) ->
    with_book_options (options), ->
      chord_book best_fingering: not options.combined, pages: options.pages

program
  .command('fingerings')
  .description('Fingerings for a specific chord')
  .action (options) ->
    with_book_options (options), ->
      chord_fingerings_page Chords[6], 'F'

program
  .command('intervals')
  .description('Intervals on the fretboard')
  .option('--pages <count>', 'Limit to this many pages', parseInt)
  .action (options) ->
    with_book_options (options), ->
      intervals_book by_root: true, pages: options.pages

program
  .command('animation')
  .description('Flipbook showing the animations of a chord')
  .option('--quick', 'Quick test -- bigger animation skips; fewer pages')
  .option('--speed <f>', 'Slower by a factor of <f>', parseFloat)
  .action (options) ->
    with_book_options (options), ->
      chord_shape_flipbook pages: 0, quick: options.quick, speed: options.speed

program
  .command('all')
  .action (options) ->
    with_book_options (options), ->
      intervals_book by_root: true
      intervals_book by_root: false
      chord_book best_fingering: true
      chord_book best_fingering: false
      chord_shape_flipbook quick: true
      # FIXME this doesn't actually render in this context
      chord_fingerings_page Chords[6], 'F'

program
  .command('*')
  .action ->
    console.log 'Commands:'
    console.log '  chordbook'
    console.log '  fingerings'
    console.log '  intervals'
    console.log '  animation'

program.parse(process.argv)
